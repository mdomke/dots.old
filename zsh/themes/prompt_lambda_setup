function prompt_lambda_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_lambda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_lambda_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_LAMBDA_COLORS=(
      "%F{33}"  # 1 blue
      "%F{136}" # 2 yellow
      "%F{125}" # 3 magenta
      "%F{160}" # 4 red
      "%F{64}"  # 5 green
      "%F{166}" # 6 orange
      "%F{61}"  # 7 violet
    )
  else
    __PROMPT_LAMBDA_COLORS=(
      "%F{blue}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{orange}"
      "%F{violet}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_LAMBDA_COLORS[7]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_LAMBDA_COLORS[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_LAMBDA_COLORS[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_LAMBDA_COLORS[6]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_LAMBDA_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b%d%a%m%u'

  # python
  zstyle ':prezto:module:python:info:virtualenv' format "${__PROMPT_LAMBDA_COLORS[1]}[%v]%f"

  PROMPT="${__PROMPT_LAMBDA_COLORS[1]}λ%f "
  RPROMPT="${__PROMPT_LAMBDA_COLORS[1]}%2~%f "'$git_info[prompt]'" "'$python_info[virtualenv]'""
}

prompt_lambda_setup "$@"
